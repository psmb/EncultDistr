# Json helper objects
prototype(Sfi.Encult:JsonObject) < prototype(TYPO3.TypoScript:RawArray) {
    @process.1 = ${Json.stringify(value)}
}
prototype(Sfi.Encult:JsonObjectRenderer) < prototype(TYPO3.TypoScript:RawArray) {
    @process.1 = ${Json.stringify(value)}
    @process.2 = ${'"' + this.id + '": ' + value}
    @process.3 = ${iterator.isLast ? value : value + ','}
}
prototype(Sfi.Encult:JsonArrayRenderer) < prototype(TYPO3.TypoScript:RawArray) {
    @process.1 = ${Json.stringify(value)}
    @process.2 = ${iterator.isLast ? value : value + ','}
}

# Rendering object for all json endponts
json = TYPO3.TypoScript:Http.Message {
	httpResponseHead {
		headers.Content-Type = 'application/json;charset=utf-8'
	}
	content = TYPO3.TypoScript:Case {
		worldviews {
			condition = ${q(node).property('uriPathSegment') == 'worldviews'}
			type = 'Sfi.Encult:WorldviewsJson'
		}
		worldview {
			condition = ${q(node).is('[instanceof Sfi.Encult:Worldview]')}
			type = 'Sfi.Encult:WorldviewJson'
		}
		questions {
			condition = ${q(node).property('uriPathSegment') == 'questions'}
			type = 'Sfi.Encult:QuestionsJson'
		}
		answers {
			condition = ${q(node).is('[instanceof Sfi.Encult:Question]')}
			type = 'Sfi.Encult:AnswersJson'
		}
		blogs {
			condition = ${q(node).property('uriPathSegment') == 'blogs'}
			type = 'Sfi.Encult:BlogsJson'
		}
		blog {
			condition = ${q(node).is('[instanceof Sfi.Encult:Blog]')}
			type = 'Sfi.Encult:BlogJson'
		}
	}
}


prototype(Sfi.Encult:WorldviewsJson) < prototype(TYPO3.TypoScript:Collection) {
	collection = ${q(node).children('[instanceof Sfi.Encult:Worldview]').get()}
	itemRenderer = Sfi.Encult:WorldviewJsonRenderer
	itemName = 'node'
	iterationName = 'iterator'

	@process.1 = ${'{' + value + '}'}
	@cache {
		mode = 'cached'
		entryTags {
			1 = ${'NodeType_Sfi.Encult:Answer'}
		}
	}
}

prototype(Sfi.Encult:WorldviewJsonRenderer) < prototype(Sfi.Encult:JsonObjectRenderer) {
	id = ${q(node).property('uriPathSegment')}
	title = ${String.stripTags(q(node).property('title'))}
	voteCount = ${q(site).find('[instanceof Sfi.Encult:Answer]').filterByReference('worldview', node).reduce('previousValue + currentValue.properties.voteCount', 0)}
}

prototype(Sfi.Encult:WorldviewJson) < prototype(Sfi.Encult:JsonObject) {
	id = ${q(node).property('uriPathSegment')}
	title = ${String.stripTags(q(node).property('title'))}
	teaser = ${String.stripTags(q(node).property('teaser'))}
	description = ${q(node).property('description')}
	voteCount = ${q(site).find('[instanceof Sfi.Encult:Answer]').filterByReference('worldview', node).reduce('previousValue + currentValue.properties.voteCount', 0)}
}




prototype(Sfi.Encult:QuestionsJson) < prototype(TYPO3.TypoScript:Collection) {
	collection = ${q(node).children('[instanceof Sfi.Encult:Question]').get()}
	itemRenderer = Sfi.Encult:QuestionJsonRenderer
	itemName = 'node'
	iterationName = 'iterator'
	@process.1 = ${'{' + value + '}'}
	@cache {
		mode = 'cached'
		entryTags {
			1 = ${'DescendantOf_' + node.identifier}
		}
	}
}

prototype(Sfi.Encult:QuestionJsonRenderer) < prototype(Sfi.Encult:JsonObjectRenderer) {
	id = ${q(node).property('uriPathSegment')}
	title = ${String.stripTags(q(node).property('title'))}
	subTitle = ${String.stripTags(q(node).property('subTitle'))}
	voteCount = ${String.toInteger(q(node).children('[instanceof Sfi.Encult:Answer]').reduce('previousValue + currentValue.properties.voteCount', 0))}
	answerWithMaxVotesIndex = ${q(node).children('[instanceof Sfi.Encult:Answer]').reduce('array[previousValue].properties.voteCount < array[index].properties.voteCount ? index : previousValue', 0)}
	answerWithMaxVotesCount = ${q(node).children('[instanceof Sfi.Encult:Answer]').get(this.answerWithMaxVotesIndex).properties.voteCount}
	worldviewWithMaxVotes = ${q(node).children('[instanceof Sfi.Encult:Answer]').get(this.answerWithMaxVotesIndex).properties.worldview.properties.title}
}







prototype(Sfi.Encult:AnswersJson) < prototype(TYPO3.TypoScript:Collection) {
	collection = ${q(node).children('[instanceof Sfi.Encult:Answer]').get()}
	itemRenderer = Sfi.Encult:AnswerJsonRenderer
	itemName = 'node'
	iterationName = 'iterator'
	@process.1 = ${'[' + value + ']'}
	@cache {
		mode = 'cached'
		entryTags {
			1 = ${'DescendantOf_' + node.identifier}
		}
	}
}

prototype(Sfi.Encult:AnswerJsonRenderer) < prototype(Sfi.Encult:JsonArrayRenderer) {
	id = ${node.identifier}
	authorName = ${String.stripTags(q(node).property('authorName'))}
	authorTitle = ${String.stripTags(q(node).property('authorTitle'))}
	quizText = ${q(node).property('quizText')}
	fullVideo = ${q(node).property('fullVideo')}
	fullVideoThumb = ${q(node).property('fullVideoThumb')}
	fullText = ${q(node).property('fullText')}
	voteCount = ${String.toInteger(q(node).property('voteCount'))}
	worldviewId = ${q(node).property('worldview').properties.uriPathSegment}
}





# Blogs

prototype(Sfi.Encult:BlogsJson) < prototype(TYPO3.TypoScript:Collection) {
	collection = ${q(node).children('[instanceof Sfi.Encult:Blog]').get()}
	itemRenderer = Sfi.Encult:BlogJsonRenderer
	itemName = 'node'
	iterationName = 'iterator'
	@process.1 = ${'{' + value + '}'}
}

prototype(Sfi.Encult:BlogJsonRenderer) < prototype(Sfi.Encult:JsonObjectRenderer) {
	id = ${q(node).property('uriPathSegment')}
	title = ${String.stripTags(q(node).property('title'))}
	date = ${q(node).property('date')}
	authorName = ${String.stripTags(q(node).property('authorName'))}
	authorTitle = ${String.stripTags(q(node).property('authorTitle'))}
	teaser = ${q(node).property('teaser')}
}

prototype(Sfi.Encult:BlogJson) < prototype(Sfi.Encult:JsonObject) {
	id = ${q(node).property('uriPathSegment')}
	title = ${String.stripTags(q(node).property('title'))}
	date = ${q(node).property('date')}
	authorName = ${String.stripTags(q(node).property('authorName'))}
	authorTitle = ${String.stripTags(q(node).property('authorTitle'))}
	teaser = ${q(node).property('teaser')}
	bodytext = TYPO3.TypoScript:Collection {
		collection = ${q(node).children('main').children()}
		itemRenderer = TYPO3.Neos:ContentCase
		itemName = 'node'
	}
}
